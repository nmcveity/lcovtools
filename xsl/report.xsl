<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns="http://www.w3.org/1999/xhtml">
<xsl:preserve-space elements="*"/>
<xsl:output method="html"/>
<xsl:template match="/">
<xsl:variable name="globalCoverage">
    <xsl:value-of select="floor((/CoverageReport/Summary/@totalVisited div /CoverageReport/Summary/@totalValid) * 100)"/>
</xsl:variable>
<HTML>
<HEAD>
<TITLE>Coverage: <xsl:value-of select="$globalCoverage"/>%</TITLE>
<STYLE type="text/css">
@import url("lua.css");

TH						{ font-weight:normal; color:#FFFFFF; background-color:#888888; }
TD.visited		        { color:#000000; }
TD.notVisited	        { background-color:#FF4040; }
TD.valid           		{ color:#000000; }
TD.notValid				{ color:#A0A0A0;  }
TD.src					{ padding: 1px }
CODE.visited            { text-indent: 8px }
CODE.notValid           { color:#A0A0A0; text-indent: 8px }
CODE.notVisited			{ background-color: #FF4040; text-indent: 8px }
TD.contents             { border-collapse: collapse; border: 1px solid #000000 }
TABLE.contents          { border-collapse: collapse; border: 1px solid #000000 }


TD.indicator_green      { background-color:#00FF00; padding: 0px}
TD.indicator_red        { background-color:#FF0000; }
TABLE.indicator         { padding: 0px; border-spacing: 0px; border: none; border-collapse: collapse }


SPAN.good				{ color:#006666; font-weight:bold; }
SPAN.critical			{ color:#880000; font-weight:bold; }
</STYLE>
</HEAD>
<BODY>
	<H1>Coverage Report</H1>
	<H2>Overview</H2>
    <BLOCKQUOTE>
        <TABLE>
            <TR>
                <TD><B>Source coverage:</B></TD><TD><xsl:value-of select="$globalCoverage"/>%</TD>
                <xsl:if test="/CoverageReport/Revision">
                    <TD><EM>(<xsl:value-of select="$globalCoverage - floor((/CoverageReport/Revision/@totalVisited div /CoverageReport/Revision/@totalValid) * 100)"/>%)</EM></TD>
                </xsl:if>
            </TR>
            <TR><TD><B>Total line count:</B></TD><TD><xsl:value-of select="/CoverageReport/Summary/@totalLines"/></TD></TR>
            <TR><TD><B>Total valid lines:</B></TD><TD><xsl:value-of select="/CoverageReport/Summary/@totalValid"/></TD></TR>
            <TR><TD><B>Total visited lines:</B></TD><TD><xsl:value-of select="/CoverageReport/Summary/@totalVisited"/></TD></TR>
        </TABLE>
        <xsl:if test="/CoverageReport/SummaryGraph">
            <IMG>
                <xsl:attribute name="src"><xsl:value-of select="/CoverageReport/SummaryGraph/text()"/></xsl:attribute>
            </IMG>
        </xsl:if>
    </BLOCKQUOTE>
	<H2>Files</H2>
    <BLOCKQUOTE>
	<TABLE class="contents">
		<xsl:for-each select="/CoverageReport/File">
            <xsl:sort select="FileSummary/@coverage" data-type="number" order="descending"/>
            <TR>
            <TD class="contents">
                <A>
				    <xsl:attribute name="href">#<xsl:value-of select="@anchor"/></xsl:attribute>
				    <xsl:value-of select="@name"/>
			    </A>
            </TD>
            <TD class="contents" width="200px" valign="middle">
                <xsl:call-template name="CoverageIndicator"/>
            </TD>
            <TD class="contents" align="right" width="40px">
                <xsl:choose>
                    <xsl:when test="./FileSummary/@coverage"><xsl:value-of select="./FileSummary/@coverage"/></xsl:when>
                    <xsl:otherwise>0</xsl:otherwise>
                </xsl:choose>
                <xsl:text>%</xsl:text>
            </TD>
            </TR>
		</xsl:for-each>
	</TABLE>
    </BLOCKQUOTE>
    <xsl:if test="//Line">
        <xsl:apply-templates select="/CoverageReport/File"/>
    </xsl:if>
    <P>Generated by lcovtools in <xsl:value-of select="/CoverageReport/Summary/@timeTaken"/> seconds.</P>
</BODY>
</HTML>
</xsl:template>

<xsl:template name="CoverageIndicator">
    <xsl:variable name="coverage">
        <xsl:choose>
            <xsl:when test="./FileSummary/@coverage"><xsl:value-of select="./FileSummary/@coverage"/></xsl:when>
            <xsl:otherwise>0</xsl:otherwise>
        </xsl:choose>
    </xsl:variable>
    <TABLE align="center" class="indicator" width="200px">
        <TR height="20px">
            <TD class="indicator_green">
                <xsl:attribute name="width"><xsl:value-of select="$coverage"/>%</xsl:attribute>
            </TD>
            <xsl:if test="$coverage &lt; 100">
                <TD class="indicator_red">
                    <xsl:attribute name="width"><xsl:value-of select="100-$coverage"/>%</xsl:attribute>
                </TD>
            </xsl:if>
        </TR>
    </TABLE>
</xsl:template>

<xsl:template match="File">
    <H2><A><xsl:attribute name="name"><xsl:value-of select="@anchor"/></xsl:attribute>File <xsl:value-of select="@name"/></A></H2>
    <TABLE class="src" width="80%" cellspacing="0" frame="vsides">
        <xsl:choose>
            <xsl:when test="PerfectCoverage">
                Full coverage
            </xsl:when>
            <xsl:when test="@missingResults">
                <B>No coverage!</B>
            </xsl:when>
            <xsl:otherwise>
                <xsl:for-each select="Line">
                    <xsl:variable name="lineClass">
                        <xsl:choose>
                            <xsl:when test="@valid = 'True'">valid</xsl:when>
                            <xsl:otherwise>notValid</xsl:otherwise>
                        </xsl:choose>
                    </xsl:variable>
                    <xsl:variable name="codeClass">
                        <xsl:choose>
                            <xsl:when test="not(@valid = 'True')">notValid</xsl:when>
                            <xsl:when test="@visited = 'True'">visited</xsl:when>
                            <xsl:otherwise>notVisited</xsl:otherwise>
                        </xsl:choose>
                    </xsl:variable>
                    <xsl:element name="TR">
                        <TD class="src" width="32px" align="center"><xsl:attribute name="class"><xsl:value-of select="$lineClass"/></xsl:attribute><CODE><xsl:value-of select="@no"/></CODE></TD>
                        <TD class="src"><xsl:attribute name="class"><xsl:value-of select="$codeClass"/></xsl:attribute><CODE>
                            <xsl:attribute name="class"><xsl:value-of select="$codeClass"/></xsl:attribute>
                            <xsl:copy-of select="."/>
                        </CODE></TD>
                    </xsl:element>
                </xsl:for-each>
            </xsl:otherwise>
        </xsl:choose>
    </TABLE>
</xsl:template>

</xsl:stylesheet>
